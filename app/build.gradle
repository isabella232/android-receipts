apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.frogermcs.androiddevmetrics'

apply from: 'credentials.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        applicationId "tech.receipts.android"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 191
        versionName "1.9.1"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "boolean", "USE_CRASHLYTICS", "false"

        buildConfigField 'String', 'SHARED_PREFS_PASSWORD', '"' + getSharedPrefsPassword() + '"'
        buildConfigField 'String', 'API_LOGIN', '"' + getApiLogin() + '"'
        buildConfigField 'String', 'API_PASSWORD', '"' + getApiPassword() + '"'
        buildConfigField 'String', 'API_CLIENT', '"' + getApiClient() + '"'
        buildConfigField 'String', 'API_CLIENT_PASSWORD', '"' + getApiClientPassword() + '"'
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " Debug"
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        disable 'InvalidPackage', 'ResourceType'
    }
}

final SUPPORT_LIBRARY_VERSION = '25.0.1'
final RETROFIT_VERSION = '2.1.0'
final OKHTTP_VERSION = '3.4.2'
final DAGGER_VERSION = '2.8'
final SQLBRITE_VERSION = '0.8.0'
final TIMBER_VERSION = '4.3.1'
final RXANDROID_VERSION = '1.2.1'
final RXJAVA_VERSION = '1.2.3'
final ABOUT_VERSION = '1.1.1'
final JODA_VERSION = '2.9.6'
final BUTTERKNIFE_VERSION = '8.4.0'
final TOASTER_VERSION = '0.3.0'

dependencies {
    def daggerCompiler = "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    // App Dependencies
    compile "com.github.medyo:android-about-page:$ABOUT_VERSION"

    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"

    compile ("com.squareup.sqlbrite:sqlbrite:$SQLBRITE_VERSION") {
        exclude module: 'support-annotations'
        exclude module: 'rxjava'
    }
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"
    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile ("com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION") {
        exclude module: 'rxjava'
    }

    compile ("io.reactivex:rxandroid:$RXANDROID_VERSION") {
        exclude module: 'rxjava'
    }
    compile "io.reactivex:rxjava:$RXJAVA_VERSION"
    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"


    compile "com.jakewharton.timber:timber:$TIMBER_VERSION"
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    compile "com.shamanland:xdroid-toaster:$TOASTER_VERSION"

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2

    // APT dependencies
    apt daggerCompiler
    apt "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    //start old
    compile('com.weiwangcn.betterspinner:library-material:1.1.0') {
        exclude group: 'com.android.support'
    }
    compile ('com.jpardogo.materialtabstrip:library:1.1.1') {
        exclude group: 'com.android.support'
    }

    compile "joda-time:joda-time:$JODA_VERSION"
    compile 'org.apache.commons:commons-lang3:3.5'
    compile ('commons-validator:commons-validator:1.5.1'){
        exclude group: 'commons-logging'
    }
}
